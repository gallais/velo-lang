The \Velo{} \acs*{repl} will let users reduce terms down to head-normal forms.
%
We can realise \Velo{}'s dynamic semantics either through definitional
interpreters~\cite{10.1145/3093333.3009866,Augustsson1999edt},
or by providing a more traditional syntactic proof of
type-soundness~\cite{DBLP:journals/iandc/WrightF94}
but mechanised~\cite[Part 2: Properties]{plfa22.08} using inductive families.

We chose the latter approach: by using inductive families, we can make explicit
our language's operational semantics.
%
This enables us to study its meta-theoretical properties and in particular prove
a progress result: every term is either a value or can take a reduction step.
%
By repeatedly applying the progress result, until we either reach a value or the end
user runs out of patience and kills the process, this proof freely gives us an
evaluator that is guaranteed to be correct with respect to \Velo{}'s operational
semantics.

Following existing approaches~\cite[Part 2: Properties]{plfa22.08}, we have defined
inductive families describing how terms reduce.

\begin{Verbatim}
data Redux : (this,that : Term metas ctxt type) -> Type where
  SimplifyCall : (p    : Prim tys ty)
              -> (step : Reduxes these those)
                      -> Redux (Call p these) (Call p those)

  ReduceAddZW : (value : Value right)
                      -> Redux (Call Add [Call Zero [], right]) right

  RewriteEqNatPW : (valueL : Value (Call Plus [this]))
                -> (valueR : Value right)
                          -> Redux (Call Add [Call Plus [this], right])
                                   (Call Add [this, Call Plus [right]])

  ReduceFuncApp : \{body  : Term metas (ctxt :< type) return\}
               -> \{var   : Term metas ctxt type\}
               -> (value : Value var)
                        -> Redux (Call App [Fun body, var])
                                 (Single.subst var body)
\end{Verbatim}

As can be seen above, our setting enforces call-by-value: $(0 + r)$, as described by the rule \IdrisData{ReduceAddZW} only reduces to $r$ if $r$ is already known to be a value.
%
Furthermore, our algebraic design (\Cref{sec:design:constants}) allows
us to easily enforce a left-to-right evaluation order by having a generic
family describing how primitive operations' arguments reduce.
%
As can be seen below: when considering a type-aligned list of arguments,
either the \IdrisBound{hd} takes a step and the \texttt{rest} is unchanged,
or the \IdrisBound{hd} is already known to be a value and a further argument
is therefore allowed to take a step.

\begin{Verbatim}
data Reduxes : (these, those : All (Term metas ctxt) tys) -> Type
  where (!:) : (hd   : Redux this that)
            -> (rest : All (Term metas ctxt) tys)
                    -> Reduxes (this :: rest) (that :: rest)

        (::) : (value : Value hd)
            -> (tl    : Reduxes these those)
                     -> Reduxes (hd :: these) (hd :: those)
\end{Verbatim}


We differ, however, from standard approaches by genericsing our proofs of progress such that the boilerplate for computing the reflexive transitive closure when reducing terms is tidied away in a shareable module.
%
Our top-level progress definition is thus parameterised by reduction and value definitions:

\begin{Verbatim}
data Progress : (0 value : Pred a) -> (0 redux : Rel a) -> (tm : a) -> Type
  where Done : \{0 tm : a\} -> (val : value tm) -> Progress value redux tm

        Step : \{this, that : a\}
            -> (step : redux this that) -> Progress value redux this
\end{Verbatim}

\noindent
and the result of execution, which is similarly parameterised, is as follows:

\begin{Verbatim}
data Result : (0 value : Pred a) -> (0 redux : Rel a) -> (this : a) -> Type
  where R : (that : a) -> (val : value that)
         -> (steps : RTList redux this that) -> Result value redux this
\end{Verbatim}

The benefit of our approach is that language designers need only to provide details of what reductions are, and how to compute a single reduction, the rest comes for free.
%
Moreover, with the result of evaluation we also get the list of reduction steps made that can, optionally, be printed to show a trace of execution.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../paper"
%%% End:
