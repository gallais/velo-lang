\subsection{Efficient Decidable Equality}
\label{sec:idioms:decEq}

Users do not always have access to a meta-program that supports the deriving of a proof that propositional equality is decidable for their data structures~\cite{DBLP:conf/icfp/ChristiansenB16}.
%
In such cases, the most common strategy is to use nested pattern matching and produce
clauses quadratic in the number of constructors for the specified data structure.
%
Na{\"i}vely one can reduce the number of contradictions to prove using symmetry breaking (\IdrisFunction{negEqSym}), but the number of cases to present is still many.
%
We can, however, reduce the complexity of \IdrisType{DecEq} instance creation from quadratic to linear in the number of constructors.
%

For example, consider the following standard definition of a binary tree:

\begin{Verbatim}
data Bin = Leaf | Node Bin Bin
\end{Verbatim}

\noindent
We first define a diagonal relation (\IdrisType{Diag}) that requires that two terms must have the same top-level constructor.

\begin{Verbatim}
data Diag : (s, t : Bin) -> Type where
  Leaf2 : Diag Leaf Leaf
  Node2 : (s, t, u, v : Bin) -> Diag (Node s t) (Node u v)
\end{Verbatim}

\noindent
Using \IdrisType{Diag} we can define a function, \IdrisFunction{diag}, function that, from two terms, either returns a proof that they satisfy the \IdrisType{Diag} relation or return \IdrisData{Nothing}.

\begin{Verbatim}
diag : (s, t : Bin) -> Maybe (Diag s t)
diag Leaf       Leaf       = Just Leaf2
diag (Node s t) (Node u v) = Just (Node2 s t u v)
diag _          _          = Nothing
\end{Verbatim}

\noindent
We can easily prove that \IdrisFunction{diag} cannot possibly return \IdrisData{Nothing} if the inputs are in fact equal.

\begin{Verbatim}
diagNot : (t : Bin) -> Not (diag t t === Nothing)
diagNot Leaf       = absurd
diagNot (Node _ _) = absurd
\end{Verbatim}

\noindent
We can use \IdrisFunction{diagNot} to implement \IdrisFunction{decEq}
by only considering cases where the two input terms share the same
top-level constructor.
For the remaining cases we only require a generic catch-all case that handles all top-level mismatches thanks to \IdrisFunction{diagNot}.

\begin{Verbatim}
decEq : (s, t : Bin) -> Dec (s === t)
decEq s@_ t@_ with (diag s t) proof eq
  _ | Just Leaf2 = Yes Refl
  _ | Just (Node2 a b u v) with (decEq a u) | (decEq b v)
    _ | Yes eq1 | Yes eq2 = Yes (cong2 Node eq1 eq2)
    _ | No neq1 | _ = No (\textbackslash{}case Refl => neq1 Refl)
    _ | _ | No neq2 = No (\textbackslash{}case Refl => neq2 Refl)
  _ | Nothing = No (\textbackslash{} Refl => diagNot _ eq)
\end{Verbatim}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../paper"
%%% End:
